plugins {
    id "fr.brouillard.oss.gradle.jgitver" version "0.10.0-rc01"
    id "net.ltgt.errorprone" version "1.1.1"
    id "java"
    id "idea"
    id "org.owasp.dependencycheck" version "5.2.4"
    id 'io.franzbecker.gradle-lombok' version "3.2.0"
    id "com.diffplug.gradle.spotless" version "3.26.0"
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

group = "de.freitag.stefan.alexa"

compileJava {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RoomBooker'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

ext {
    errorproneVersion='2.3.2'
}

dependencies {
    errorprone('com.google.errorprone:error_prone_core:' + errorproneVersion)

    implementation('com.amazon.alexa:ask-sdk:2.26.0',
            'com.amazonaws:aws-lambda-java-core:1.2.0',
            'com.amazonaws:aws-java-sdk-ses:1.11.354',
            'com.amazonaws:aws-lambda-java-log4j2:1.1.0',
            'org.apache.logging.log4j:log4j-api:2.8.2',
            'org.apache.logging.log4j:log4j-core:2.8.2',
            'com.google.guava:guava:28.1-jre',
            'com.ullink.slack:simpleslackapi:1.2.0'
    )
}

jgitver {
    mavenLike true
}

wrapper{
    gradleVersion = '6.0.1'
    distributionType = Wrapper.DistributionType.BIN
}


dependencyCheck {
    failBuildOnCVSS=6
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all"
    options.errorprone {
        disableWarningsInGeneratedCode = true
        ignoreUnknownCheckNames = true
        allErrorsAsWarnings = true
        errorproneArgs = ['-Xep:ParameterName:OFF',
                          '-Xep:FallThrough:OFF',
                          '-Xep:FunctionalInterfaceClash:OFF',
                          '-Xep:AssertionFailureIgnored:OFF',
                          '-Xlint:unchecked',
                          '-Xlint:deprecation'
        ]
    }
}

lombok {
    version = "1.18.10"
    sha256 = "2836e954823bfcbad45e78c18896e3d01058e6f643749810c608b7005ee7b2fa"
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}